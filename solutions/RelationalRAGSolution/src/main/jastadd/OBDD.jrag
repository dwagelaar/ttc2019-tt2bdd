aspect OBDD {

  syn BDD TruthTable.OBDD() {
    return OBDDImpl();
  }

  private BDD TruthTable.OBDDImpl() {

    BDD bdd = emptyBDD();

    Map<String, BDD_Leaf> terminalNodeMap = terminalNodeMap(bdd);

    PortOrder portOrder = getPortOrder();

    BDD_Subtree root = new BDD_Subtree();
    root.setPort(bdd.bddInputPort(portOrder.getPortList().get(0)));
    bdd.addTree(root);
    bdd.setRoot(root);

    Port leafPort = portOrder.leafPort();
    for (Row row : getRowList()) {
      insertRow(bdd, terminalNodeMap, root, row, 0);
    }

    return bdd;
  }

  private void TruthTable.insertRow(BDD bdd, Map<String, BDD_Leaf> terminalNodeMap, BDD_Subtree parent, Row row, int position) {

    InputPort port = getPortOrder().getPortList().get(position);
    Boolean value = row.valueFor(port);

    if (position < getPortOrder().getPortList().size() - 1) {
      BDD_InputPort nextPort = bdd.bddInputPort(getPortOrder().getPortList().get(position + 1));
      if (value == null || value) {
        BDD_Subtree result;
        if (parent.getTreeForOne() != null) {
          result = parent.getTreeForOne().asSubtree();
        } else {
          result = new BDD_Subtree();
          result.setPort(nextPort);
          parent.setTreeForOne(result);
          bdd.addTree(result);
        }
        insertRow(bdd, terminalNodeMap, result, row, position + 1);
      }
      if (value == null || !value) {
        BDD_Subtree result;
        if (parent.getTreeForZero() != null) {
          result = parent.getTreeForZero().asSubtree();
        } else {
          result = new BDD_Subtree();
          result.setPort(nextPort);
          parent.setTreeForZero(result);
          bdd.addTree(result);
        }
        insertRow(bdd, terminalNodeMap, result, row, position + 1);
      }
    } else {
      BDD_Leaf terminal = terminalNodeMap.get(row.outputString());
      terminal.addRow(row);
      if (value == null) {
        if (parent.getTreeForOne() != null || parent.getTreeForZero() != null) {
          logger.warn("found duplicate row");
        }
        parent.setTreeForOne(terminal);
        parent.setTreeForZero(terminal);
      } else if (value) {
        if (parent.getTreeForOne() != null) {
          logger.warn("found duplicate row");
        }
        parent.setTreeForOne(terminal);
      } else {
        if (parent.getTreeForZero() != null) {
          logger.warn("found duplicate row");
        }
        parent.setTreeForZero(terminal);
      }
    }
  }

  private Map<String, BDD_Leaf> TruthTable.terminalNodeMap(BDD bdd) {
    Map<String, BDD_Leaf> result = new HashMap<>(getNumRow());
    for (Row row: getRowList()) {
      String outputString = row.outputString();
      if (!result.containsKey(outputString)) {
        BDD_Leaf terminal = new BDD_Leaf();
        for (Cell cell: row.getCellList()) {
          Port cellPort = cell.getPort();
          if (!cellPort.isInput()) {
            BDD_Assignment a = new BDD_Assignment();
            a.setPort(bdd.bddOutputPort(cellPort.asOutput()));
            a.setValue(cell.getValue());
            terminal.addAssignment(a);
            bdd.addTree(terminal);
          }
        }
        result.put(row.outputString(), terminal);
      }
    }
    return result;
  }
}
