-- @atlcompiler emftvm
-- @nsURI TT=https://www.transformation-tool-contest.eu/2019/tt
-- @nsURI BDD=https://www.transformation-tool-contest.eu/2019/bdd
module TT2BDD;
create OUT:BDD from IN:TT;

rule TruthTable2BDD {
   from src : TT!TruthTable
   to dest : BDD!BDD
      (
         name <- src.name,
         ports <- src.ports,
         tree <- src.tree.cell
      )
}

rule InputPort2InputPort {
   from src : TT!InputPort
   to dest : BDD!InputPort
      (
         name <- src.name
      )
}

rule OutputPort2OutputPort {
   from src : TT!OutputPort
   to dest : BDD!OutputPort
      (
         name <- src.name
      )
}

rule Row2Leaf {
   from src : TT!Row
   to dest : BDD!Leaf
      (
         assignments <- src.cells->select(c | c.port.oclIsKindOf(TT!OutputPort))
      )
}

rule Cell2Assignment {
   from src: TT!Cell (src.port.oclIsKindOf(TT!OutputPort))
   to dest : BDD!Assignment
      (
         value <- src.value,
         port <- src.port
      )
}

rule Cell2Subtree {
   from src: TT!Cell (not src.node.oclIsUndefined())
   to dest : BDD!Subtree
      (
         port <- src.port,
         treeForZero <-
            if src.node.zeroSubtree.oclIsKindOf(TT!Row) then
               src.node.zeroSubtree
            else
               src.node.zeroSubtree.cell
            endif,
         treeForOne <-
            if src.node.oneSubtree.oclIsKindOf(TT!Row) then
               src.node.oneSubtree
            else
               src.node.oneSubtree.cell
            endif
      )
}

---------------------------------------------------------------------------------------------------
-- Partition one part of the truth table.
--
-- This helper performs some preparatory work before the creation of a binary tree.
-- It takes a subsequence of the rows of a truth table and a certain port, and returns
-- two subsequences of rows: cells for which the port is false, and cells for which the
-- port is true.
--
-- Inputs:
--    rows : the collection of lines to be processed.
--    port : the port which should act as a reference for the partitioning.
--
-- Outputs:
--    a tuple that contains:
--       zeroPart : the collection of rows for which the port is 0 (false)
--       onePart  : the collection of rows for which the port is 1 (true)
--
-- Preconditions :
--
--    The port to be used must be an input port :
--
--       port.oclIsKindOf(TT!InputPort))
--
--    The value is defined for all rows:
--
--         rows->forAll(r |
--            r.cells->collect(c | c.port)
--                   ->includes(port)
--         )
---------------------------------------------------------------------------------------------------
helper def:
   getPartition(rows : Sequence(TT!Row), port : TT!Port)
      : TupleType( zeroPart : Sequence(TT!Row) , onePart : Sequence(TT!Row) ) =
      
      -- Select the rows for which the port is false
      let _zeroPart : Sequence(TT!Row) =
         rows->reject(r |
            r.falseCellsByPort.get(port).oclIsUndefined()
         ) in
         
      -- Select the rows for which the port is true
      let _onePart : Sequence(TT!Row) =
         rows->reject(r |
            r.trueCellsByPort.get(port).oclIsUndefined()
         ) in

      -- Build the resulting tuple
      Tuple{
         zeroPart = _zeroPart,
         onePart = _onePart
      };

helper context TT!Row def: trueCellsByPort : Map(TT!Port, Set(TT!Cell)) =
	self.cells
		->select(c | c.value)
		->mappedBy(c | c.port);

helper context TT!Row def: falseCellsByPort : Map(TT!Port, Set(TT!Cell)) =
	self.cells
		->reject(c | c.value)
		->mappedBy(c | c.port);

---------------------------------------------------------------------------------------------------
-- Build a tree structure from a sequence of rows.
--
-- This helper builds a tree from tuples. Among the sequence of usable inputs, it selects a
-- port where the value is defined in all lines.
--
-- The helper getPartition is invoked to obtain two row subsequences that correspond to the
-- possible states from the selected port.
--
-- The tree is built recursively, with the base case leaving the row sequence as a single row.
--
-- Inputs:
--    rows        : the sequence of rows to be processed
--    usablePorts : the sequence of ports to be considered to build the tree
--
-- Outputs:
--    a tuple contains:
--      cell        : a cell which acts as a reference and points to the current tree node
--      zeroSubtree : the subtree for the 0 value of the port
--      oneSubtree  : the subtree for the 1 value of the port
---------------------------------------------------------------------------------------------------
helper def:
   getTree(rows : Sequence(TT!Row), usablePorts : Sequence(TT!Port))
      : TupleType( cell : TT!Cell , zeroSubtree : OclAny , oneSubtree : OclAny ) =

      -- Among the usable ports, select one where the value is defined in all rows
      let _port : TT!Port =
         usablePorts->any(p |
            rows->forAll(r |
               r.cells->collect(c | c.port)
                     ->includes(p)
            )
         ) in
      
      -- Select a cell which defines a value for the port
      let _cell : TT!Cell =
         rows->first().cells->any(c | c.port = _port) in
         
      -- Partition the provided collection of rows
      let _part : TupleType( zeroPart : Sequence(TT!Row), onePart : Sequence(TT!Row) ) =
         thisModule.getPartition(rows, _port) in

      -- Define the new collection of usable ports for the resulting partitionings
      let _updatedPorts : Sequence(TT!Port) =
         usablePorts->excluding(_port) in
            
      -- Build the resulting tuple : the tree structure is created recursively
      Tuple{
         cell = _cell,
         zeroSubtree =
            if _part.zeroPart->size() = 1 then
               _part.zeroPart->first()
            else
               thisModule.getTree(_part.zeroPart, _updatedPorts)
            endif,
         oneSubtree =
            if _part.onePart->size() = 1 then
               _part.onePart->first()
            else
               thisModule.getTree(_part.onePart, _updatedPorts)
            endif
      };

---------------------------------------------------------------------------------------------------
-- Obtain the tree which represents an entire truth table.
--
-- This helper builds a tree structure using thisModule.getTree().
---------------------------------------------------------------------------------------------------
helper context TT!TruthTable def:
   tree
      : TupleType( cell : TT!Cell , zeroSubtree : OclAny , oneSubtree : OclAny ) =
      thisModule.getTree(self.rows, self.ports->select(p | p.oclIsKindOf(TT!InputPort)));
      
---------------------------------------------------------------------------------------------------
-- Search for a truth table cell within a tree
--
-- This helper maps all tree nodes by their truth table cell. It uses the collectAllNodes
-- helper to flatten the tree of nodes into a Sequence.
--
-- Inputs :
--    self : the truth table for which to search the tree
--
-- Outputs :
--    the Map of cells to tree nodes
---------------------------------------------------------------------------------------------------
helper context TT!TruthTable def: 
	nodesByCell
		: Map(TT!Cell, TupleType( cell : TT!Cell, zeroSubtree : OclAny, oneSubtree : OclAny )) =
	
		thisModule.collectAllNodes(self.tree)
			->mappedBySingle(node | node.cell);

helper def:
	collectAllNodes(tree : TupleType( cell : TT!Cell, zeroSubtree : OclAny, oneSubtree : OclAny ))
		: Sequence(TupleType( cell : TT!Cell , zeroSubtree : OclAny , oneSubtree : OclAny )) =

		if tree.zeroSubtree.oclIsKindOf(TT!Row) then
			if tree.oneSubtree.oclIsKindOf(TT!Row) then
            	-- Both subtrees are leaf nodes
            	Sequence{}
         	else
            	-- Only the subtree 1 is not a leaf
            	thisModule.collectAllNodes(tree.oneSubtree)
			endif
		else
			if tree.oneSubtree.oclIsKindOf(TT!Row) then
            	-- Only the subtree 0 is not a leaf
            	thisModule.collectAllNodes(tree.zeroSubtree)
			else
            	-- Both subtrees are non-leaves
				thisModule.collectAllNodes(tree.zeroSubtree)->union(
					thisModule.collectAllNodes(tree.oneSubtree))
			endif
		endif
		->prepend(tree);

---------------------------------------------------------------------------------------------------
-- Obtain, for a given truth table cell, the matching node within the corresponding
-- binary decision tree.
--
-- This helper uses nodesByCell.
---------------------------------------------------------------------------------------------------
helper context TT!Cell def:
	node 
		: TupleType( cell : TT!Cell , zeroSubtree : OclAny , oneSubtree : OclAny ) =

      	self.owner.owner.nodesByCell.get(self);
